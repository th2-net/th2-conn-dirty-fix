plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'com.palantir.docker' version '0.25.0'
}

apply plugin: 'application'
apply plugin: 'com.palantir.docker'
apply plugin: 'kotlin-kapt'

group 'com.exactpro.th2'
version release_version

sourceCompatibility = 11
targetCompatibility = 11

ext {
    sharedDir = file("${project.rootDir}/shared")
}

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()

    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.1.0')

    implementation 'com.exactpro.th2:common:5.1.0-dev-version-5-4085018593-8adee33-SNAPSHOT'
    implementation 'com.exactpro.th2:netty-bytebuf-utils:0.0.1'
    implementation('com.exactpro.th2:conn-dirty-tcp-core:3.0.0-dev-version-3-4230391407-SNAPSHOT') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        because 'Projects should use only slf4j-api, without coupling to a certain implementation'
    }
    implementation 'com.exactpro.th2:grpc-lw-data-provider:2.0.0-dev-version-2-4112857941-SNAPSHOT'

    implementation 'org.slf4j:slf4j-api'
    implementation 'io.github.microutils:kotlin-logging:2.1.23'

    implementation 'io.netty:netty-all:4.1.86.Final'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21'
    implementation 'com.google.auto.service:auto-service:1.0.1'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jackson_version
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_version
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jackson_version
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jackson_version

    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5:1.7.10'

    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'
    kapt 'com.google.auto.service:auto-service:1.0.1'
}

test {
    useJUnitPlatform()
}

application {
    mainClassName 'com.exactpro.th2.conn.dirty.tcp.core.Main'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}
